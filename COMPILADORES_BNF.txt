Grupo:
        Renan Soares, 41327853
        Felipe Rocha, 31418864
        Claudio Ribeiro, 41308859
        Caio Boratto, 31328334

PROG   -> MAIN {CLASSE}
MAIN   -> class ID '{' public static void main ( String [ ] ID ) '{' CMD '}' '}'
CLASSE -> class ID [extends ID] '{' {VAR} {METODO} '}'
VAR    -> TIPO ID ;
METODO -> public TIPO ID '(' [PARAMS] ')' '{' {VAR} {CMD} return EXP ; '}'
PARAMS -> TIPO ID {, TIPO ID}
TIPO   -> int '[' ']'
        | boolean
        | int
        | ID
CMD    -> '{' {CMD} '}'
        | if '(' EXP ')' CMD
        | if '(' EXP ')' CMD else CMD
        | while '(' EXP ')' CMD
        | System.out.println '(' EXP ')' ;
        | ID = EXP ;
        | ID '[' EXP ']' = EXP ;
EXP    -> EXP && REXP
        | REXP
REXP   -> REXP < AEXP
        | REXP == AEXP
        | REXP != AEXP
        | AEXP
AEXP   -> AEXP + MEXP
        | AEXP - MEXP
        | MEXP
MEXP   -> MEXP * SEXP
        | SEXP
SEXP   -> ! SEXP
        | - SEXP
        | true
        | false
        | NUMS
	| null
        | new int '[' EXP ']'
        | PEXP . length
        | PEXP '[' EXP ']'
        | PEXP
PEXP   -> ID
        | this
        | new ID '(' ')'
        | '(' EXP ')'
        | PEXP . ID
        | PEXP . ID '(' [EXPS] ')'
EXPS   -> EXP {, EXP}
ID     -> LETTS {LETTS | NUMS | '_'}
LETTS  -> LETT {LETT}
LETT   -> 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'Q' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z'
	| 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z'
NUMS   -> NUM {NUM}
NUM    -> 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
